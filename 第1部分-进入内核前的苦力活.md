**Linux0.11整体流程：**

![图片](assets/640-1705312283006.png)









# 第一部分完结 进入内核前的苦力活

## 1.最开始的两行代码

### 1.1 BIOS加载启动区

你按下开机键的那一刻，在主板上提前写死的固件程序 **BIOS** 会将硬盘中**启动区的 512 字节**的数据，原封不动复制到**内存中的 0x7c00** 这个位置，并跳转到那个位置进行执行。

```c
启动区的定义:
	只要硬盘中的 0 盘 0 道 1 扇区的 512 个字节的最后两个字节分别是 0x55 和 0xaa，那么 BIOS就会认为它是个启动区。
```

![图片](assets\640-rs.png)



**所以作为操作系统的开发人员，仅仅需要把操作系统最开始的那段代码，编译并存储在硬盘的 0 盘 0 道 1 扇区即可**。之后 BIOS 会帮我们把它放到内存里，并且跳过去执行。



而 Linux-0.11 的最开始的代码，就是这个用汇编语言写的 **bootsect.s**，位于 **boot** 文件夹下。

![图片](assets\640-1705290123341-rs.png)

通过编译，这个 bootsect.s 会被编译成二进制文件，存放在启动区的第一扇区。

![图片](assets\640-1705290209867-rs.png)





### 1.2 ds寄存器初赋值

就从这个文件的第一行代码开始

`Linux-0.11\boot\bootsect.s`

```assembly
entry start
start:
	mov	ax,#BOOTSEG  ; BOOTSEG  = 0x07c0	
	mov	ds,ax
```

这段代码是用汇编语言写的，含义是把 0x07c0 这个值复制到 **ax 寄存器**里，再将 ax 寄存器里的值复制到 **ds 寄存器**里。那其实这一番折腾的结果就是，让 ds 这个寄存器里的值变成了 0x07c0。

![图片](assets/640-1705290424365-rs.png)

```assembly
ds 是一个 16 位的段寄存器，具体表示数据段寄存器，在内存寻址时充当段基址的作用。
```





**此时内存状态：**

![图片](assets/640-1705290733952-rs.png)









## 2.自己给自己挪个地儿

### 2.1 复制启动区

继续往下看几行

`Linux-0.11\boot\bootsect.s`

```assembly
mov	ax,#INITSEG  ; INITSEG  = 0x9000
mov	es,ax
mov	cx,#256
sub	si,si
sub	di,di
rep
movw
```

经过这些指令后，以下几个寄存器分别被附上了指定的值

![1705290961365](assets/1705290961365.png)

**此时CPU 寄存器的总图：**

![图片](assets/640-1705290924465-rs.png)

干嘛要给这些毫不相干的寄存器附上值呢？其实就是为下2条指令服务的，就是

```assembly
rep movw
```

`rep movw`：**不断重复地复制一个字：**

- **重复执行多少次呢？**
  - 是 `cx 寄存器`中的值，也就是 256 次
- **从哪复制到哪呢？**
  - 是从 `ds:si `处复制到` es:di `处。
- **一次复制多少呢？**
  - 复制一个字，16 位，也就是两个字节

直译：**将内存地址 0x7c00 处开始往后的 512 字节的数据，原封不动复制到 0x90000 处**。

![图片](assets/640-1705291409894-rs.png)





### 2.2 跳转到 INITSEG:go 执行

再往后是一个**跳转**指令

`Linux-0.11\boot\bootsect.s`

```assembly
jmpi	go,INITSEG  ; INITSEG  = 0x9000
```

**jmpi** 是一个**段间跳转指令**，表示跳转到 **0x9000:go** 处执行。

![图片](assets/640-1705291936333-rs.png)











## 3.做好最最基础的准备工作

### 3.1 内存初步规划(配置各类相关寄存器)

跟着 CPU 的步伐往后看

`Linux-0.11\boot\bootsect.s`

```assembly
go:	mov	ax,cs
	mov	ds,ax
	mov	es,ax
; put stack at 0x9ff00.
	mov	ss,ax
	mov	sp,#0xFF00		; arbitrary value >>512
```

这段代码的直接意思很容易理解，就是把 **cs** 寄存器的值分别复制给 **ds**、**es** 和 **ss** 寄存器，然后又把 0xFF00 给了 **sp** 寄存器。

**寄存器赋值：**

- `cs 寄存器`(**代码段寄存器**)
  - CPU 当前正在执行的代码在内存中的位置，就是由 `cs:ip `这组寄存器配合指向的，其中` cs `是基址，`ip` 是偏移地址
  - 于之前执行过一个段间跳转指令`jmpi go,0x9000`，所以现在 cs 寄存器里的值就是 **0x9000**，ip 寄存器里的值是 **go** 这个标签的偏移地址
- `ds寄存器`(**数据段寄存器**)
- `es寄存器`（**扩展段寄存器**）
- `ss寄存器`（**栈段寄存器**）
  - 配合栈基址寄存器 `sp` 来表示此时的**栈顶地址**。而此时 `sp 寄存器`被赋值为了 **0xFF00** 了，所以目前的栈顶地址就是 `ss:sp` 所指向的地址 `0x9FF00` 处。
- `sp寄存器`（**栈基址寄存器** ）

![图片](assets/640-1705300318076-rs.png)

总结拔高一下，这一部分其实就是把**代码段寄存器 cs**，**数据段寄存器 ds**，**栈段寄存器 ss** 和**栈基址寄存器 sp** 分别设置好了值，方便后续使用。



操作系统在做的事情，就是给如何**访问代码**，如何**访问数据**，如何**访问栈**进行了一下**内存的初步规划**。

- 访问代码和访问数据的规划方式就是设置了一个**基址**
- 访问栈就是把**栈顶指针**指向了一个远离代码位置的地方

![图片](assets/640-1705300387834-rs.png)











## 4.把自己在硬盘里的其他部分也放到内存来

### 4.1 load setup

做好这些基础工作后，接下来就又该新的一翻折腾了，我们接着往下看。

`Linux-0.11\boot\bootsect.s`

```assembly
; load the setup-sectors directly after the bootblock.
; Note that 'es' is already set up.

load_setup:
	mov	dx,#0x0000		; drive 0, head 0
	mov	cx,#0x0002		; sector 2, track 0
	mov	bx,#0x0200		; address = 512, in INITSEG
	mov	ax,#0x0200+SETUPLEN	; service 2, nr of sectors
	int	0x13			; read it
	jnc	ok_load_setup		; ok - continue
	mov	dx,#0x0000
	mov	ax,#0x0000		; reset the diskette
	int	0x13
	j	load_setup

ok_load_setup:
	...
```

`int 0x13` 表示**0x13 号中断**，CPU 会通过这个**中断号**，去寻找对应的**中断处理程序的入口地址**，并**跳转**过去执行，逻辑上就相当于**执行了一个函数**。

- 这条指令上面给 `dx、cx、bx、ax `赋值都是作为这个中断程序的参数。
- ` 0x13号中断`的处理程序是 BIOS 提前给我们写好的，是**读取磁盘**的相关功能的函数。

本段代码的注释已经写的很明确了，直接说最终的作用吧，**就是将硬盘的第 2 个扇区开始，把数据加载到内存 0x90200 处，共加载 4 个扇区**，图示其实就是这样。

![图片](assets/640-1705311954625-rs.png)

可以看到，如果复制成功，就跳转到 **ok_load_setup** 这个标签，如果失败，则会不断重复执行这段代码，也就是重试。









### 4.2 load system

那我们就别管重试逻辑了，直接看成功后跳转的 ok_load_setup 这个标签后的代码。

`Linux-0.11\boot\bootsect.s`

```assembly
ok_load_setup:
    ...
    mov ax,#0x1000
    mov es,ax       ; segment of 0x10000
    call read_it
    ...
    jmpi 0,SETUPSEG  ; SETUPSEG = 0x9020
```

这段代码省略了很多非主逻辑的代码，比如在屏幕上输出 Loading system ... 这个字符串以防止用户等烦了。

剩下的主要代码就都写在这里了，就这么几行，其作用是**把从硬盘第 6 个扇区开始往后的 240 个扇区，加载到内存 0x10000 处**

![图片](assets/640-1705312752963-rs.png)

至此，整个操作系统的**全部代码**，就已经全部从硬盘中，被搬迁到内存来了。







### Notes-操作系统的编译过程

整个编译过程，就是通过 **Makefile** 和 **build.c** 配合完成的，最终会： 

1. 把 `bootsect.s` 编译成 `bootsect` 放在硬盘的 1 扇区。
2. 把 `setup.s` 编译成 `setup` 放在硬盘的 2~5 扇区。
3. 把剩下的`全部代码（head.s 作为开头）`编译成 `system` 放在硬盘的随后 240 个扇区。

所以整个路径就是这样的。

![图片](assets/640-1705318846671-rs.png)

所以，我们即将跳转到的内存中的 **0x90200** 处的代码，就是从硬盘第二个扇区开始处加载到内存的。第二个扇区的最开始处，那也就是 setup.s 文件的第一行代码咯。

![图片](assets/640-1705319119332.png)











## 5.进入保护模式前的最后一次折腾内存

### 5.1 读取光标位置到dx寄存器

操作系统已经完成了各种从硬盘到内存的加载，以及内存到内存的复制。至此，整个 `bootsect.s` 的使命就完成了。之后便跳转到了 **0x90200** 这个位置开始执行，这个位置处的代码就是位于 `setup.s`的开头。

`Linux-0.11\boot\setup.s`

```assembly
entry start
start:
	mov	ax,#INITSEG	; this is done in bootsect already, but...
	mov	ds,ax
	mov	ah,#0x03	; read cursor pos
	xor	bh,bh
	int	0x10		; save it in known place, con_init fetches
	mov	[0],dx		; it from 0x90000.
```

这个 `int 0x10`  是触发 BIOS 提供的**显示服务**中断处理程序，而 `ah 寄存器`被赋值为 **0x03** 表示显示服务里具体的**读取光标位置功能**。

`int 0x10`中断程序执行完毕并返回时，`dx 寄存器`里的值表示**光标的位置**（具体说来其高八位 dh 存储了**行号**，低八位 dl 存储了列号。）

![图片](assets/640-1705319589164-rs.png)

那下一步 `mov [0],dx` 就是把这个光标位置存储在 `[0]` 这个内存地址处。

```assembly
注意，前面我们说过，这个内存地址仅仅是偏移地址，还需要加上 ds 这个寄存器里存储的段基址，最终的内存地址是在 0x90000 处，这里存放着光标的位置，以便之后在初始化控制台的时候用到。
```









### 5.2 读取其它硬件参数到内存

再接下来的几行代码，都是和刚刚一样的逻辑，调用一个 BIOS 中断获取点什么信息，然后存储在内存中某个位置

`Linux-0.11\boot\setup.s`

```assembly
; Get memory size (extended mem, kB)
    mov ah,#0x88
    int 0x15
    mov [2],ax

; Get video-card data: (获取显卡显示模式)
    mov ah,#0x0f
    int 0x10
    mov [4],bx      ; bh = display page
    mov [6],ax      ; al = video mode, ah = window width

; check for EGA/VGA and some config parameters(检查显示方式并取参数)
    mov ah,#0x12
    mov bl,#0x10
    int 0x10
    mov [8],ax
    mov [10],bx
    mov [12],cx
    
; Get hd0 data(获取第一块硬盘的信息)
    mov ax,#0x0000
    mov ds,ax
    lds si,[4*0x41]
    mov ax,#INITSEG
    mov es,ax
    mov di,#0x0080
    mov cx,#0x10
    rep
    movsb
    
; Get hd1 data(获取第二块硬盘的信息)
    mov ax,#0x0000
    mov ds,ax
    lds si,[4*0x46]
    mov ax,#INITSEG
    mov es,ax
    mov di,#0x0090
    mov cx,#0x10
    rep
    movsb
```

以上原理都是一样的。



我们就没必要细琢磨了，对操作系统的理解作用不大，只需要知道最终存储在内存中的信息是什么，在什么位置，就好了，之后会用到他们的。 

| 内存地址 | 长度(字节) | 名称         |
| -------- | ---------- | ------------ |
| 0x90000  | 2          | 光标位置     |
| 0x90002  | 2          | 扩展内存数   |
| 0x90004  | 2          | 显示页面     |
| 0x90006  | 1          | 显示模式     |
| 0x90007  | 1          | 字符列数     |
| 0x90008  | 2          | 未知         |
| 0x9000A  | 1          | 显示内存     |
| 0x9000B  | 1          | 显示状态     |
| 0x9000C  | 2          | 显卡特性参数 |
| 0x9000E  | 1          | 屏幕行数     |
| 0x9000F  | 1          | 屏幕列数     |
| 0x90080  | 16         | 硬盘1参数表  |
| 0x90090  | 16         | 硬盘2参数表  |
| 0x901FC  | 2          | 根设备号     |





### 5.3 关闭中断

把这些信息存储好之后，操作系统又要做什么呢？我们继续往下看。

`Linux-0.11\boot\setup.s`

```assembly
; now we want to move to protected mode ...
	cli			; no interrupts allowed ;
```

就一行 cli，表示**关闭中断**的意思。

因为后面我们要把原本是 BIOS 写好的中断向量表给覆盖掉，也就是给破坏掉了，写上我们自己的中断向量表，所以这个时候是不允许中断进来的。





### 5.4 复制system到内存起始地址

继续看。

`Linux-0.11\boot\setup.s`

```assembly
; first we move the system to it's rightful place

	mov	ax,#0x0000
	cld			; 'direction'=0, movs moves forward
do_move:
	mov	es,ax		; destination segment
	add	ax,#0x1000
	cmp	ax,#0x9000
	jz	end_move
	mov	ds,ax		; source segment
	sub	di,di
	sub	si,si
	mov 	cx,#0x8000
	rep
	movsw
	jmp	do_move

; then we load the segment descriptors

end_move:
	...
```

**rep movsw** 做了个内存复制操作，最终的结果是，把内存地址 **0x10000** 处开始往后一直到 **0x90000** 的内容，统统复制到内存的最开始的 **0** 位置，大概就是这么个效果。

![图片](assets/640-1705320513943-rs.png)

由于之前的各种加载和复制，导致内存看起来很乱，是时候进行一波取舍和整理了，我们重新梳理一下此时的内存布局。

栈顶地址仍然是 **0x9FF00** 没有改变。

**0x90000** 开始往上的位置，原来是 **bootsect** 和 **setup** 程序的代码，现 bootsect 的一部分代码在已经被操作系统为了记录内存、硬盘、显卡等一些**临时存放的**数据给覆盖了一部分。

内存最开始的 **0** 到 **0x80000** 这 512K 被 **system** 模块给占用了，之前讲过，这个 system 模块就是除了 bootsect 和 setup 之外的全部程序链接在一起的结果，可以理解为**操作系统的全部**。

那么现在的内存布局就是这个样子。

![图片](assets/640-1705320797129-rs.png)









## 6.先解决段寄存器的历史包袱问题

### 6.1 load idt & gdt

接下来就要进行真正的第一项大工程了，那就是**模式的转换**，需要从现在的 16 位的**实模式**转变为之后 32 位的**保护模式**。

`Linux-0.11\boot\setup.s`

```assembly
end_move:
	mov	ax,#SETUPSEG	; right, forgot this at first. didn't work :-)
	mov	ds,ax
	lidt	idt_48		; load idt with 0,0
	lgdt	gdt_48		; load gdt with whatever appropriate


gdt:
	.word	0,0,0,0		; dummy

	.word	0x07FF		; 8Mb - limit=2047 (2048*4096=8Mb)
	.word	0x0000		; base address=0
	.word	0x9A00		; code read/exec
	.word	0x00C0		; granularity=4096, 386

	.word	0x07FF		; 8Mb - limit=2047 (2048*4096=8Mb)
	.word	0x0000		; base address=0
	.word	0x9200		; data read/write
	.word	0x00C0		; granularity=4096, 386

idt_48:
	.word	0			; idt limit=0
	.word	0,0			; idt base=0L

gdt_48:
	.word	0x800		; gdt limit=2048, 256 GDT entries
	.word	512+gdt,0x9	; gdt base = 0X9xxxx
```

<mark>**接下来的Notes均为对这段代码的解释**</mark>







### 6.2 lgdt gdt_48详解

**全局描述符表（gdt）**长什么样？它在哪？怎么让 CPU 知道它在哪？ 



先说说它在哪？**在内存中**呗，那么怎么告诉 CPU 全局描述符表（gdt）在内存中的什么位置呢？答案是由操作系统把这个位置信息存储在一个叫 **gdtr** 的寄存器中。

![图片](assets/640-1705321765279-rs.png)

怎么存呢？就是刚刚那条指令。

```assembly
lgdt    gdt_48
```

**lgdt** 就表示把**后面的值（gdt_48）**放在 **gdtr** 寄存器中，gdt_48 标签，我们看看

```assembly
gdt_48:
	.word	0x800		; gdt limit=2048, 256 GDT entries
	.word	512+gdt,0x9	; gdt base = 0X9xxxx
```

可以看到这个标签位置处表示一个 48 位的数据，其中高 32 位存储着的正是全局描述符表 gdt 的内存地址`0x90200 + gdt`

**gdt** 是个标签，表示在本文件内的偏移量，而本文件是 **setup.s**，编译后是放在 **0x90200** 这个内存地址的，还记得吧？所以要加上 0x90200 这个值。

![图片](assets/640-1705370031200-rs.png)





那 gdt 这个标签处，就是全局描述符表在内存中的真正数据了。

```assembly
gdt:
    .word   0,0,0,0     ; dummy

    .word   0x07FF      ; 8Mb - limit=2047 (2048*4096=8Mb)
    .word   0x0000      ; base address=0
    .word   0x9A00      ; code read/exec
    .word   0x00C0      ; granularity=4096, 386

    .word   0x07FF      ; 8Mb - limit=2047 (2048*4096=8Mb)
    .word   0x0000      ; base address=0
    .word   0x9200      ; data read/write
    .word   0x00C0      ; granularity=4096, 386
```

根据刚刚的段描述符格式。

![图片](assets/640-1705321342423-rs.png)

可以看出目前全局描述符表有三个段描述符：

1. 第一个为**空**
2. 第二个是**代码段描述符（type=code）**
3. 第三个是**数据段描述符（type=data）**

此时的`全局描述符表gdt`

![图片](assets/640-1705370172025-rs.png)



接下来我们看看目前的内存布局，还是别管比例。

![图片](assets/640-1705370399216-rs.png)

 **idt** 这个是**中断描述符表**，用来在发生中断时，CPU 拿着中断号去中断描述符表中寻找中断处理程序的地址，找到后就跳到相应的中断程序中去执行。**idtr** 寄存器，指向 **idt**







### Notes-实模式CPU下计算物理地址的方式

![图片](assets/640-1705321198119-rs.png)

是不是觉得很别扭，那更别扭的地方就要来了。当 CPU 切换到**保护模式**后，同样的代码，内存地址的计算方式还不一样







### Notes-保护模式下CPU 计算物理地址的方式

变成啥样了呢？刚刚那个 ds 寄存器里存储的值，在实模式下叫做**段基址**，在保护模式下叫**段选择子**。**段选择子里存储着段描述符的索引。**

![图片](assets/640-1705321267213-rs.png)

通过段描述符索引，可以从**全局描述符表 gdt** 中找到一个段描述符，**段描述符里存储着段基址**。

![图片](assets/640-1705321342423-rs.png)

段基址取出来，再和偏移地址相加，就得到了物理地址，整个过程如下。

![图片](assets/640-1705321388147-rs.png)

总结一下就是，**段寄存器（比如 ds、ss、cs）里存储的是段选择子，段选择子去全局描述符表中寻找段描述符，从中取出段基址**。









## 7.六行代码就进入了保护模式

### 7.1 打开 A20 地址线

第6回仅仅是进入保护模式前准备工作的其中一个，我们接着往下看。代码仍然是 setup.s 中的。

`Linux-0.11\boot\setup.s`

```assembly
; that was painless, now we enable A20

	call	empty_8042
	mov	al,#0xD1		; command write
	out	#0x64,al
	call	empty_8042
	mov	al,#0xDF		; A20 on
	out	#0x60,al
	call	empty_8042
```

这段代码的意思是，**打开 A20 地址线**。



简单理解，这一步就是为了突破地址信号线 20 位的宽度，变成 32 位可用。当 CPU 到了 32 位时代之后，由于要考虑**兼容性**，还必须保持一个只能用 20 位地址线的模式，所以如果你不手动开启的话，即使地址线已经有 32 位了，仍然会限制只能使用其中的 20 位。(8086CPU历史问题)







### 7.2 reprogram the interrupts

接下来的一段代码，你完全完全不用看

`Linux-0.11\boot\setup.s`

```assembly
; well, that went ok, I hope. Now we have to reprogram the interrupts :-(
; we put them right after the intel-reserved hardware interrupts, at
; int 0x20-0x2F. There they won't mess up anything. Sadly IBM really
; messed this up with the original PC, and they haven't been able to
; rectify it afterwards. Thus the bios puts interrupts at 0x08-0x0f,
; which is used for the internal hardware interrupts as well. We just
; have to reprogram the 8259's, and it isn't fun.

	mov	al,#0x11		; initialization sequence
	out	#0x20,al		; send it to 8259A-1
	.word	0x00eb,0x00eb		; jmp $+2, jmp $+2
	out	#0xA0,al		; and to 8259A-2
	.word	0x00eb,0x00eb
	mov	al,#0x20		; start of hardware int's (0x20)
	out	#0x21,al
	.word	0x00eb,0x00eb
	mov	al,#0x28		; start of hardware int's 2 (0x28)
	out	#0xA1,al
	.word	0x00eb,0x00eb
	mov	al,#0x04		; 8259-1 is master
	out	#0x21,al
	.word	0x00eb,0x00eb
	mov	al,#0x02		; 8259-2 is slave
	out	#0xA1,al
	.word	0x00eb,0x00eb
	mov	al,#0x01		; 8086 mode for both
	out	#0x21,al
	.word	0x00eb,0x00eb
	out	#0xA1,al
	.word	0x00eb,0x00eb
	mov	al,#0xFF		; mask off all interrupts for now
	out	#0x21,al
	.word	0x00eb,0x00eb
	out	#0xA1,al
```

这里是对**可编程中断控制器 8259 芯片**进行的编程。



因为中断号是不能冲突的， Intel 把 `0 `到 `0x19` 号中断都作为**保留中断**，比如 0 号中断就规定为**除零异常**，软件自定义的中断都应该放在这之后，但是 IBM 在原 PC 机中搞砸了，跟保留中断号发生了冲突，以后也没有纠正过来，所以我们得重新对其进行编程，不得不做，却又一点意思也没有。这是 Linus 在上面注释上的原话。



所以我们也不必在意，只要知道重新编程之后，8259 这个**芯片的引脚**与**中断号**的对应关系，变成了如下的样子就好。

| PIC 请求号 | 中断号 | 用途         |
| ---------- | ------ | ------------ |
| IRQ0       | 0x20   | 时钟中断     |
| IRQ1       | 0x21   | 键盘中断     |
| IRQ2       | 0x22   | 接连从芯片   |
| IRQ3       | 0x23   | 串口2        |
| IRQ4       | 0x24   | 串口1        |
| IRQ5       | 0x25   | 并口2        |
| IRQ6       | 0x26   | 软盘驱动器   |
| IRQ7       | 0x27   | 并口1        |
| IRQ8       | 0x28   | 实时钟中断   |
| IRQ9       | 0x29   | 保留         |
| IRQ10      | 0x2a   | 保留         |
| IRQ11      | 0x2b   | 保留         |
| IRQ12      | 0x2c   | 鼠标中断     |
| IRQ13      | 0x2d   | 数学协处理器 |
| IRQ14      | 0x2e   | 硬盘中断     |
| IRQ15      | 0x2f   | 保留         |







### 7.3 打开实模式&跳转到0地址执行

好了，接下来的一步，就是真正切换模式的一步了，从代码上看就两行。

`Linux-0.11\boot\setup.s`

```assembly
; well, that certainly wasn't fun :-(. Hopefully it works, and we don't
; need no steenking BIOS anyway (except for the initial loading :-).
; The BIOS-routine wants lots of unnecessary data, and it's less
; "interesting" anyway. This is how REAL programmers do it.
;
; Well, now's the time to actually move into protected mode. To make
; things as simple as possible, we do no register set-up or anything,
; we let the gnu-compiled 32-bit programs do that. We just jump to
; absolute address 0x00000, in 32-bit protected mode.

	mov	ax,#0x0001	; protected mode (PE) bit
	lmsw	ax		; This is it;
	jmpi	0,8		; jmp offset 0 of segment 8 (cs)
```

前两行，将 cr0 这个寄存器的位 0 置 1，模式就从实模式切换到保护模式了。

![图片](assets/640-1705372998545-rs.png)

再往后，又是一个段间跳转指令 **jmpi**，后面的 **8 表示 cs（代码段寄存器）的值，0 表示偏移地址**。请注意，此时已经是保护模式了，之前也说过，保护模式下内存寻址方式变了，段寄存器里的值被当做段选择子。

回顾下段选择子的模样。

![图片](assets/640-1705373445161-rs.png)

8 用二进制表示就是 `00000,0000,0000,1000`

对照上面段选择子的结构，可以知道**描述符索引值是 1**，也就是要去**全局描述符表（gdt）**中找第一项段描述符。

还记得上一讲中的全局描述符的具体内容么？

第 0 项是空值，第一项被表示为**代码段描述符**，是个可读可执行的段，第二项为**数据段描述符**，是个可读可写段，不过他们的段基址都是 0。

所以，这里取的就是这个代码段描述符，**段基址是 0**，偏移也是 0，那加一块就还是 0 咯，所以**最终这个跳转指令，就是跳转到内存地址的 0 地址处，开始执行。**



零地址处是什么呢？还是回顾之前的内存布局图。

![图片](assets/640-1705373475496-rs.png)

就是操作系统全部代码的 system 这个大模块，**system 模块**怎么生成的呢？由 Makefile 文件可知，是由 `head.s `和 `main.c` 以及`其余各模块`的操作系统代码合并来的，可以理解为操作系统的全部核心代码编译后的结果。

所以，接下来，我们就要重点阅读 **head.s** 了。

![图片](assets/640-1705373418072.png)















## 8.烦死了又要重新设置一遍 idt 和 gdt

### 8.1 head.s init

那接下来，我们就品品，正式进入 c 语言写的 main.c 之前的 **head.s** 究竟写了点啥？

`Linux-0.11\boot\head.s`

```assembly
/*
 *  head.s contains the 32-bit startup code.
 *
 * NOTE!!! Startup happens at absolute address 0x00000000, which is also where
 * the page directory will exist. The startup code will be overwritten by
 * the page directory.
 */
.text
.globl _idt,_gdt,_pg_dir,_tmp_floppy_area
_pg_dir:
startup_32:
	mov eax,0x10
    mov ds,ax
    mov es,ax
    mov fs,ax
    mov gs,ax
	lss _stack_start,%esp
```

注意到开头有个标号 **_pg_dir**。先留个心眼，这个表示**页目录**，之后在设置分页机制时，页目录会存放在这里，也会覆盖这里的代码。

再往下连续五个 **mov** 操作，分别给 ds、es、fs、gs 这几个段寄存器赋值为 **0x10**，根据段描述符结构解析，**表示这几个段寄存器的值为指向全局描述符表中的第二个段描述符，也就是数据段描述符。**

最后 **lss** 指令相当于让 **ss:esp** 这个栈顶指针指向了 **_stack_start** 这个标号的位置。还记得图里的那个原来的栈顶指针在哪里吧？往上翻一下，**0x9FF00**，现在要变咯。



这个 `_stack_start `标号定义在了很久之后才会讲到的 **sched.c** 里，我们这里拿出来分析一波。

```assembly
long user_stack[4096 >> 2];

struct
{
  long *a;
  short b;
}
stack_start = {&user_stack[4096 >> 2], 0x10};
```

这啥意思呢？

首先，`stack_start `结构中的高位 8 字节是 **0x10**，将会赋值给 **ss** 栈段寄存器，低位 16 字节是 **user_stack** 这个数组的最后一个元素的地址值，将其赋值给 **esp** 寄存器。

赋值给 `ss` 的 `0x10` 仍然按照保护模式下的**段选择子**去解读，其指向的是全局描述符表中的第二个段描述符（数据段描述符），段基址是 0。

赋值给 `esp 寄存器`的就是 `user_stack` 数组的**最后一个元素的内存地址值**，那最终的**栈顶地址**，也指向了这里（user_stack + 0），后面的压栈操作，就是往这个新的栈顶地址处压咯。







### 8.2 setup idt & gdt

继续往下看

`Linux-0.11\boot\head.s`

```assembly
	call setup_idt
	call setup_gdt
	mov eax,10h
    mov ds,ax
    mov es,ax
    mov fs,ax
    mov gs,ax
	lss _stack_start,%esp
```

先设置了 **idt** 和 **gdt**，然后又重新执行了一遍刚刚执行过的代码。

为什么要重新设置这些段寄存器呢？因为上面修改了 gdt，所以要重新设置一遍以刷新才能生效。那我们接下来就把目光放到设置 idt 和 gdt 上。





### 8.3 setup_idt详解

中断描述符表 idt 我们之前没设置过，所以这里设置具体的值，理所应当。

`Linux-0.11\boot\head.s`

```assembly
/*
 *  setup_idt
 *
 *  sets up a idt with 256 entries pointing to
 *  ignore_int, interrupt gates. It then loads
 *  idt. Everything that wants to install itself
 *  in the idt-table may do so themselves. Interrupts
 *  are enabled elsewhere, when we can be relatively
 *  sure everything is ok. This routine will be over-
 *  written by the page tables.
 */
setup_idt:
	lea ignore_int,%edx
	movl $0x00080000,%eax
	movw %dx,%ax		/* selector = 0x0008 = cs */
	movw $0x8E00,%dx	/* interrupt gate - dpl=0, present */

	lea _idt,%edi
	mov $256,%ecx
rp_sidt:
	movl %eax,(%edi)
	movl %edx,4(%edi)
	addl $8,%edi
	dec %ecx
	jne rp_sidt
	lidt idt_descr
	ret


idt_descr:
	.word 256*8-1		# idt contains 256 entries
	.long _idt

_idt:
    DQ 256 dup(0)
```

不用细看，我给你说最终效果。

中断描述符表 idt 里面存储着一个个中断描述符，每一个中断号就对应着一个中断描述符，而中断描述符里面存储着主要是中断程序的地址，这样一个中断号过来后，CPU 就会自动寻找相应的中断程序，然后去执行它。

<mark>那这段程序的作用就是，**设置了 256 个中断描述符**，并且让每一个中断描述符中的中断程序例程都指向一个 **ignore_int** 的函数地址，这个是个**默认的中断处理程序**，之后会逐渐被各个具体的中断程序所覆盖。</mark>比如之后键盘模块会将自己的键盘中断处理程序，覆盖过去。

那现在，产生任何中断都会指向这个默认的函数 ignore_int，也就是说现在这个阶段**你按键盘还不好使**。





### 8.4 setup_gdt详解

设置中断描述符表 `setup_idt` 说完了，那接下来 **setup_gdt** 就同理了。我们就直接看设置好后的新的全局描述符表长什么样吧？

`Linux-0.11\boot\head.s`

```assembly
setup_gdt:
	lgdt gdt_descr
	ret
	
gdt_descr:
	.word 256*8-1		# so does gdt (not that that's any
	.long _gdt		# magic number, but it works for me :^)

	.align 3

_gdt:	
	.quad 0x0000000000000000	/* NULL descriptor */
	.quad 0x00c09a0000000fff	/* 16Mb */
	.quad 0x00c0920000000fff	/* 16Mb */
	.quad 0x0000000000000000	/* TEMPORARY - don't use */
	.fill 252,8,0			/* space for LDT's and TSS's etc */
```

其实和我们原先设置好的 gdt 一模一样。

也是有**代码段描述符**和**数据段描述符**，然后第四项系统段描述符并没有用到，不用管。最后还留了 252 项的空间，这些空间后面会用来放置**任务状态段描述符 TSS** 和**局部描述符 LDT**，这个后面再说。

![图片](assets/640-1705377484161-rs.png)



为什么原来已经设置过一遍了，这里又要重新设置一遍，你可千万别想有什么复杂的原因，就是因为原来设置的 gdt 是在 setup 程序中，之后这个地方要被缓冲区覆盖掉，所以这里重新设置在 head 程序中，这块内存区域之后就不会被其他程序用到并且覆盖了，就这么个事。

![图片](assets/640-1705377511183-rs.png)









## 9.Intel 内存管理两板斧：分段与分页

### 9.1 开启分页&跳转到main函数

书接上回，上回书咱们说到，head.s 代码在重新设置了 gdt 与 idt 后。来到了这样一段代码。

`Linux-0.11\boot\head.s`

```assembly
	jmp after_page_tables
...

after_page_tables:
	pushl $0		# These are the parameters to main :-)
	pushl $0
	pushl $0
	pushl $L6		# return address for main, if it decides to.
	pushl $_main
	jmp setup_paging
L6:
	jmp L6			# main should never return here, but
					# just in case, we know what happens.
```

那就是开启分页机制，并且跳转到 main 函数。

 





### 9.2 setup_paging之页表设置

如何跳转到之后用 c 语言写的 main.c 里的 main 函数，是个有趣的事，也包含在这段代码里。不过我们先瞧瞧这**分页机制**是如何开启的，也就是 **setup_paging** 这个标签处的代码。

`Linux-0.11\boot\head.s`

```assembly
setup_paging:
    mov ecx,1024*5
    xor eax,eax
    xor edi,edi
    pushf
    cld
    rep stosd
    mov eax,_pg_dir
    mov [eax],pg0+7
    mov [eax+4],pg1+7
    mov [eax+8],pg2+7
    mov [eax+12],pg3+7
    mov edi,pg3+4092
    mov eax,00fff007h
    std
L3: stosd
    sub eax,00001000h
    jge L3
    popf
    xor eax,eax
    mov cr3,eax
    mov eax,cr0
    or  eax,80000000h
    mov cr0,eax
    ret
```

这段代码，就是帮我们把页表和页目录表在内存中写好，之后开启 cr0 寄存器的分页开关，仅此而已。

我们先说这段代码最终产生的效果吧。



按照当前的页目录表和页表这种机制，1 个页目录表最多包含 1024 个页目录项（也就是 1024 个页表），1 个页表最多包含 1024 个页表项（也就是 1024 个页），1 页为 4KB（因为有 12 位偏移地址）总共4GB。

但是当时 linux-0.11 认为，总共可以使用的内存不会超过 **16M**，也即最大地址空间为 **0xFFFFFF**。因此，16M 的地址空间可以用 1 个页目录表 + 4 个页表搞定。

![1705403928669](assets/1705403928669.png)

所以，上面这段代码就是，**将页目录表放在内存地址的最开头**，还记得上一讲开头让你留意的 _pg_dir 这个标签吧？

```assembly
_pg_dir:
_startup_32:
    mov eax,0x10
    mov ds,ax
    ...
```

**之后紧挨着这个页目录表，放置 4 个页表**，代码里也有这四个页表的标签项。

```assembly
.org 0x1000 pg0:
.org 0x2000 pg1:
.org 0x3000 pg2:
.org 0x4000 pg3:
.org 0x5000
```

最终将页目录表和页表填写好数值，来覆盖整个 16MB 的内存。随后，开启分页机制。此时内存中的页表相关的布局如下。

![图片](assets/640-1705404816536-rs.png)

这些页目录表和页表放到了整个内存布局中最开头的位置，就是覆盖了开头的 system 代码了，不过被覆盖的 system 代码已经执行过了，所以无所谓。



同时，如 idt 和 gdt 一样，我们也需要通过一个寄存器告诉 CPU 我们把这些页表放在了哪里，就是这段代码。

```assembly
xor eax,eax
mov cr3,eax
```

你看，我们相当于告诉 cr3 寄存器，**0 地址处就是页目录表，再通过页目录表可以找到所有的页表**，也就相当于 CPU 知道了分页机制的全貌了。

至此后，整个内存布局如下。

![图片](assets/640-1705405019650-rs.png)









### 9.3 setup_paging之内存映射

那么具体页表设置好后，映射的内存是怎样的情况呢？那就要看页表的具体数据了，就是这一坨代码。

```assembly
setup_paging:
    ...
    mov eax,_pg_dir
    mov [eax],pg0+7
    mov [eax+4],pg1+7
    mov [eax+8],pg2+7
    mov [eax+12],pg3+7
    mov edi,pg3+4092
    mov eax,00fff007h
    std
L3: stosd
    sub eax, 1000h
    jpe L3
    ...
```

很简单，对照刚刚的页目录表与页表结构看。

![图片](assets/640-1705405361337-rs.png)

前五行表示，页目录表的前 4 个页目录项，分别指向 4 个页表。比如页目录项中的第一项 **[eax]** 被赋值为 **pg0+7**，也就是 **0x00001007**，根据页目录项的格式，表示页表地址为 **0x1000**，页属性为 **0x07** 表示改页存在、用户可读写。

后面几行表示，填充 4 个页表的每一项，一共 **4\*1024=4096** 项，依次映射到内存的前 16MB 空间。

画出图就是这个样子，其实刚刚的图就是。

![图片](assets/640-1705406100347-rs.png)





看，最终的效果就是，经过这套分页机制，**线性地址将恰好和最终转换的物理地址一样**。

现在只有四个页目录项，也就是将前 16M 的线性地址空间，与 16M 的物理地址空间一一对应起来了。

![图片](assets/640-1705405685742-rs.png)













### Notes-内存分页机制

还记不记得之前我们在代码中给出一个内存地址，在保护模式下要先经过分段机制的转换，才能最终变成物理地址，就是这样。

![图片](assets/640-1705396899304-rs.png)

这是在没有开启分页机制的时候，只需要经过这一步转换即可得到最终的物理地址了，但是在开启了分页机制后，又会**多一步转换**。

![图片](assets/640-1705396931983-rs.png)

也就是说，在没有开启分页机制时，由程序员给出的**逻辑地址**，需要先通过分段机制转换成物理地址。但在开启分页机制后，逻辑地址仍然要先通过分段机制进行转换，只不过转换后不再是最终的物理地址，而是**线性地址**，然后再通过一次分页机制转换，得到最终的物理地址。



分段机制我们已经清楚如何对地址进行变换了，那分页机制又是如何变换的呢？我们直接以一个例子来学习过程。



比如我们的线性地址（已经经过了分段机制的转换）是 `15M`

二进制表示就是 `0000000011_0100000000_000000000000`

我们看一下它的转换过程

![图片](assets/640-1705397050784-rs.png)

也就是说，CPU 在看到我们给出的内存地址后，首先把线性地址被拆分成

![1705397189119](assets/1705397189119.png)

1. 高 10 位负责在**页目录表**中找到一个**页目录项**，
2. 根据上一步页目录项的值加上中间 10 位拼接后的地址去**页表**中去寻找一个**页表项**
3. 根据上一步的页表项的值，再加上后 12 位偏移地址，就是最终的物理地址。

而这一切的操作，都由计算机的一个硬件叫 **MMU**，中文名字叫**内存管理单元**，有时也叫 PMMU，分页内存管理单元。由这个部件来负责将虚拟地址转换为物理地址。



所以整个过程我们不用操心，作为操作系统这个软件层，只需要提供好页目录表和页表即可。

这种页表方案叫做**二级页表**：

- 第一级叫**页目录表 PDE**，
- 第二级叫**页表 PTE**。

他们的结构如下。

![图片](assets/640-1705398407648-rs.png)



之后再开启分页机制的开关。其实就是更改 **cr0** 寄存器中的一位即可（31 位），还记得我们开启保护模式么，也是改这个寄存器中的一位的值。

![图片](assets/640-1705398610058-rs.png)

然后，MMU 就可以帮我们进行分页的转换了。此后指令中的内存地址（就是程序员提供的逻辑地址），就统统要先经过分段机制的转换，再通过分页机制的转换，才能最终变成物理地址。







### Notes-关于内存地址

关于地址，我们已经出现了好多词了，包括**逻辑地址**、**线性地址**、**物理地址**，以及本文中没出现的，你可能在很多地方看到过的**虚拟地址**。

而这些地址后面加上空间两个字，似乎又成为了一个新词，比如**线性地址空间**、**物理地址空间**、**虚拟地址空间**等。

ntel 体系结构的**内存管理**可以分成两大部分，也就是标题中的两板斧，**分段**和**分页**。

- **分段机制**在之前几回已经讨论过多次了，其目的是为了为每个程序或任务提供单独的代码段（cs）、数据段（ds）、栈段（ss），使其不会相互干扰。
- **分页机制**是本回讲的内容，开机后分页机制默认是关闭状态，需要我们手动开启，并且设置好页目录表（PDE）和页表（PTE）。其目的在于可以按需使用物理内存，同时也可以在多任务时起到隔离的作用，这个在后面将多任务时将会有所体会。

在 Intel 的保护模式下，分段机制是没有开启和关闭一说的，它必须存在，而分页机制是可以选择开启或关闭的。所以如果有人和你说，它实现了一个没有分段机制的操作系统，那一定是个外行。



再说说那些地址：

- **逻辑地址**：我们程序员写代码时给出的地址叫逻辑地址，其中包含段选择子和偏移地址两部分。
- **线性地址**：通过分段机制，将逻辑地址转换后的地址，叫做线性地址。而这个线性地址是有个范围的，这个范围就叫做线性地址空间，32 位模式下，线性地址空间就是 4G。
- **物理地址**：就是真正在内存中的地址，它也是有范围的，叫做物理地址空间。那这个范围的大小，就取决于你的内存有多大了。

![图片](assets/640-1705405943977-rs.png)

知道逻辑地址、线性地址、物理地址这三个概念就够了，逻辑地址是程序员给出的，经过分段机制转换后变成线性地址，然后再经过分页机制转换后变成物理地址，就这么简单。









## 10.进入 main 函数前的最后一跃！

### 10.1 跳转到main函数的实现

这仍然要回到上一讲我们跳转到设置分页代码的那个地方（head.s 里），这里有个骚操作帮我们跳转到 main.c。

`Linux-0.11\boot\head.s`

```assembly
after_page_tables:
    push 0
    push 0
    push 0
    push L6
    push _main
    jmp setup_paging
...
setup_paging:
    ...
    ret
```

直接解释起来非常简单。

push 指令就是**压栈**，五个 push 指令过去后，栈会变成这个样子。

![图片](assets/640-1705406248487-rs.png)

然后注意，setup_paging 最后一个指令是 **ret**，也就是我们上一回讲的设置分页的代码的最后一个指令，形象地说它叫**返回指令**，但 CPU 可没有那么聪明，它并不知道该返回到哪里执行，只是很机械地**把栈顶的元素值当做返回地址**，跳转去那里执行。



再具体说是，把` esp 寄存器`（栈顶地址）所指向的内存处的值，赋值给 `eip 寄存器`，而` cs:eip` 就是 **CPU 要执行的下一条指令的地址**。

而此时**栈顶**刚好是 main.c 里写的 **main 函数的内存地址**，是我们刚刚特意压入栈的，所以 CPU 就理所应当跳过来了。

当然 Intel CPU 是设计了 `call` 和 `ret` 这一配对儿的指令，意为**调用函数和返回**，具体可以看后面本回扩展资料里的内容。

至于其他压入栈的 L6 是用作当 main 函数返回时的跳转地址，但由于在操作系统层面的设计上，main 是绝对不会返回的，所以也就没用了。而其他的三个压栈的 0，本意是作为 main 函数的参数，但实际上似乎也没有用到，所以也不必关心。

总之，经过这一个小小的骚操作，程序终于跳转到 main.c 这个由 c 语言写就的主函数 main 里了！







### 10.2 main.c概览&初步总结

`linux-0.11\init\main.c`

```c
void main(void) {
    ROOT_DEV = ORIG_ROOT_DEV;
    drive_info = DRIVE_INFO;
    memory_end = (1<<20) + (EXT_MEM_K<<10);
    memory_end &= 0xfffff000;
    if (memory_end > 16*1024*1024)
        memory_end = 16*1024*1024;
    if (memory_end > 12*1024*1024) 
        buffer_memory_end = 4*1024*1024;
    else if (memory_end > 6*1024*1024)
        buffer_memory_end = 2*1024*1024;
    else
        buffer_memory_end = 1*1024*1024;
    main_memory_start = buffer_memory_end;
    mem_init(main_memory_start,memory_end);
    trap_init();
    blk_dev_init();
    chr_dev_init();
    tty_init();
    time_init();
    sched_init();
    buffer_init(buffer_memory_end);
    hd_init();
    floppy_init();
    sti();
    move_to_user_mode();
    if (!fork()) {
        init();
    }
    for(;;) pause();
}
```

没错，这就是这个 main 函数的全部了。而整个操作系统也会最终停留在最后一行死循环中，永不返回，直到关机。



好了，至此，整个第一部分就圆满结束了，为了跳进 main 函数的准备工作，我称之为进入内核前的苦力活，就完成了！我们看看我们做了什么。

![图片](assets/640-1705407171524-rs.png)

我把这些称为**进入内核前的苦力活**，经过这样的流程，内存被搞成了这个样子。

![图片](assets/640-1705407191216-rs.png)

之后，main 方法就开始执行了，靠着我们辛辛苦苦建立起来的内存布局，向崭新的未来前进！









## 第一部分总结

当你按下开机键的那一刻，在主板上提前写死的固件程序 BIOS 会将硬盘中启动区的 512 字节的数据，原封不动复制到内存中的 0x7c00 这个位置，并跳转到那个位置进行执行，

![图片](assets/640-1705408023622-rs.png)

有了这个步骤之后，我们就可以把代码写在硬盘第一扇区，让 BIOS 帮我们加载到内存并由 CPU 去执行，我们不用操心这个过程。

而这一个扇区的代码，就是操作系统源码中最最最开始的部分，它可以执行一些指令，也可以把硬盘的其他部分加载到内存，其实本质上也是执行一些指令。这样，整个计算机今后如何运作，就完全交到我们自己的手中，想怎么玩就怎么玩了。

这是 [第一回 | 最开始的两行代码](http://mp.weixin.qq.com/s?__biz=Mzk0MjE3NDE0Ng==&mid=2247499226&idx=1&sn=1ff3d54a3608423b7781142ae3ac62ca&chksm=c2c58777f5b20e61664a51d31ab779bbe68c317c3459218c7cd4b9530887dbb33470628c3248&scene=21#wechat_redirect) 讲的内容。





接下来，直到 [第四回 | 把自己在硬盘里的其他部分也放到内存来](http://mp.weixin.qq.com/s?__biz=Mzk0MjE3NDE0Ng==&mid=2247499359&idx=1&sn=233812a464996b9566cdf3258132bc22&chksm=c2c584f2f5b20de40a7990c754cdbf3073b4652f318d479ac0c8ff686ca7aa74eef1ba7c6c2f&scene=21#wechat_redirect)，我们才讲到整个操作系统的编译和加载过程的全貌，就是下面这张图。 

![图片](assets/640-1705408075258-rs.png)

而我们整个的第一部分，其实就在讲 boot 文件夹下的三个汇编文件的内容，`bootsect.s`，`setup.s` 以及后面要和其他全部操作系统代码做链接的 `head.s`。







前五回的内容一直在调整内存的布局，把这块内存复制到那块，又把那块内存复制到这块，所以在 [第五回 | 进入保护模式前的最后一次折腾内存](http://mp.weixin.qq.com/s?__biz=Mzk0MjE3NDE0Ng==&mid=2247499558&idx=1&sn=d5d057fae2fb835d38f82804c2649463&chksm=c2c5858bf5b20c9de2db5c31dd34fbe766f6c46cda17f54f4518a75fc1838f2ada6fe5717aad&scene=21#wechat_redirect) 的结尾，我让你记住这样一张图，在很长一段时间这个内存布局的大体框架就不会再变了，前五回的内容你也可以抛在脑后了。

![图片](assets/640-1705408134008-rs.png)









从第六回开始往后，就是逐渐进入保护模式，并设置分段、分页、中断等机制的地方。最终的内存布局变成了这个样子。

![图片](assets/640-1705408176862-rs.png)

你看，idtr 寄存器指向了 idt，这个就是中断的设置；gdtr 寄存器指向了 gdt，这个就是全局描述符表的设置，可以简单理解为分段机制的设置；cr3 寄存器指向了页目录表的位置，这个就是分页机制的设置。



中断的设置，就引出了 CPU 与操作系统处理中断的流程。 

分段和分页的设置，引出了逻辑地址到物理地址的转换。 具体来说，逻辑地址到线性地址的转换，依赖 Intel 的分段机制。而线性地址到物理地址的转换，依赖 Intel 的分页机制。 



**分段**和**分页**，就是 Intel 管理内存的两大利器，也是内存管理最最最最底层的支撑。

- **代码**
- **数据**
- **栈**

而 Intel 也提供了三个段寄存器来分别对应着三类内存：

- **代码段寄存器（cs）**
- **数据段寄存器（ds）**
- **栈段寄存器（ss）**

分段和分页，以及这几个寄存器的设置，其实本质上就是安排我们今后访问内存的方式，做了一个初步规划，**包括去哪找代码、去哪找数据、去哪找栈，以及如何通过分段和分页机制将逻辑地址转换为最终的物理地址**。

而所有上面说的这一切，和 Intel CPU 这个硬件打交道比较多，设置了一些最最最最基础的环境和内存布局，为之后进入 main 函数做了充分的准备，因为 c 语言虽然很底层了，但也有其不擅长的事情，就交给第一部分的汇编语言来做，所以我称第一部分为**进入内核前的苦力活**。











